plugins {
    id 'groovy'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.10.1"
    id "de.gliderpilot.semantic-release" version "1.4.0"
}

group 'de.javaansehz'

project.ext.ghToken = project.hasProperty('ghToken') ? project.getProperty('ghToken') : System.getenv('GH_TOKEN') ?: null
semanticRelease {
    repo {
        ghToken = project.ghToken
    }
    releaseBranches {
        include 'master'
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation group: 'com.google.inject', name: 'guice', version: '4.2.2'
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.11'
    implementation group: 'com.trivago.rta', name: 'cluecumber-report-plugin', version: '2.2.0'
}

gradlePlugin {
    plugins {
        cluecumberReportGradlePlugin {
            id = 'de.javaansehz.cluecumber-report-gradle-plugin'
            implementationClass = 'de.javaansehz.CluecumberReport'
        }
    }
}

publishing {
    repositories {
        mavenLocal()
    }
}

pluginBundle {
    website = 'https://github.com/JavaanseHZ/cluecumber-report-gradle-plugin'
    vcsUrl = 'https://github.com/JavaanseHZ/cluecumber-report-gradle-plugin'
    description = 'Gradle Wrapper for https://github.com/trivago/cluecumber-report-plugin.'
    plugins {
        cluecumberReportGradlePlugin {
            id = 'de.javaansehz.cluecumber-report-gradle-plugin'
            displayName = 'Gradle cluecumber reporting'
            tags = ['cucumber', 'reports', "test"]
        }
    }
}

if (!version.toString().endsWith('-SNAPSHOT'))
    publish.dependsOn publishPlugins
