plugins {
    id 'groovy'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.12.0"
    id "de.gliderpilot.semantic-release" version "1.4.0"
    id 'jacoco'
}

group 'de.javaansehz'

project.ext.ghToken = project.hasProperty('ghToken') ? project.getProperty('ghToken') : System.getenv('GH_TOKEN') ?: null
semanticRelease {
    repo {
        ghToken = project.ghToken
    }
    releaseBranches {
        include 'master'
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation group: 'com.google.inject', name: 'guice', version: '4.2.3'
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.11'
    implementation group: 'com.trivago.rta', name: 'cluecumber-report-plugin', version: '2.7.0'
    testCompile (group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5') {
        exclude module: "groovy-all"
    }
}

gradlePlugin {
    plugins {
        cluecumberReportGradlePlugin {
            id = 'de.javaansehz.cluecumber-report-gradle-plugin'
            displayName = 'Cluecumber Report Plugin Gradle Wrapper'
            description = 'Gradle Wrapper for https://github.com/trivago/cluecumber-report-plugin.'
            implementationClass = 'de.javaansehz.CluecumberReport'
        }
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut"
        exceptionFormat "full"
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

publishing {
    repositories {
        mavenLocal()
    }
}

pluginBundle {
    website = 'https://github.com/JavaanseHZ/cluecumber-report-gradle-plugin'
    vcsUrl = 'https://github.com/JavaanseHZ/cluecumber-report-gradle-plugin.git'
    description = 'Gradle Wrapper for https://github.com/trivago/cluecumber-report-plugin.'
    plugins {
        cluecumberReportGradlePlugin {
            id = 'de.javaansehz.cluecumber-report-gradle-plugin'
            displayName = 'Cluecumber Report Plugin Gradle Wrapper'
            tags = ['cucumber', 'reports', "test"]
        }
    }
}

if (!version.toString().endsWith('-SNAPSHOT'))
    publish.dependsOn publishPlugins
